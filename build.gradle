plugins {
    id 'java'
    id 'checkstyle'
    id 'application'
    id 'com.github.spotbugs' version '5.0.14'
    id 'jacoco'
}
repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

checkstyle {
    toolVersion = '10.3'
    configFile = file("config/checkstyle/checkstyle.xml")
}

spotbugs {
    toolVersion = '4.7.3'
    ignoreFailures = false
    effort = 'max'
    reportLevel = 'high'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport, checkstyleMain, spotbugsMain // Ensures all checks run after tests
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

spotbugsMain {
    reports {
        xml.required = false
        html.required = true
    }
}
application {
    mainClass = 'Airport.Main'
}


def isCI = System.getenv("GITHUB_ACTIONS") == "true"
if (isCI) {
    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}